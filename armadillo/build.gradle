apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'
apply plugin: 'com.github.kt3k.coveralls'
apply from: "$rootDir/gradle/jacoco-utils.gradle"
apply from: "$rootDir/gradle/checkstyle.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionNameLib

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "byte[]", "PREF_SALT", "new byte[]{0x4F, 0x00, 0x10, 0x2E, 0x62, 0x45, 0x4C, 0x0F, 0x3B, (byte) 0xF9, 0x37, 0x50, 0x47, 0x22, 0x24, 0x5D, (byte) 0xE0, (byte) 0xD8, 0x55, (byte) 0xC1, (byte) 0xBA, 0x23, (byte) 0x8D, (byte) 0xA9, (byte) 0xF0, (byte) 0xA3, 0x77, 0x4D, 0x38, 0x61, (byte) 0xF0, (byte) 0xBB};"
        buildConfigField "byte[]", "STATIC_RANDOM", "new byte[]{(byte) 0xBC, 0x1D, (byte) 0x91, 0x7B, (byte) 0xB3, (byte) 0xD7, (byte) 0xA4, 0x19, 0x2C, 0x72, 0x52, (byte) 0xF4, 0x34, (byte) 0xDC, (byte) 0xCC, 0x4F, (byte) 0xDC, (byte) 0xD8, (byte) 0xEF, 0x51, (byte) 0xF0, (byte) 0xA9, 0x5B, (byte) 0xDD, 0x62, 0x1A, 0x53, 0x10, (byte) 0xDD, 0x21, (byte) 0x83, (byte) 0xBD};"
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    api 'at.favre.lib:hkdf:1.0.2'
    api 'at.favre.lib:bytes:0.8.0'
    api 'at.favre.lib:bcrypt:0.7.0'
    api 'com.jakewharton.timber:timber:4.7.1'

    api group: 'com.android.support', name: 'support-annotations', version: rootProject.ext.dependencies.support

    testImplementation 'org.mindrot:jbcrypt:0.4'
    testImplementation "junit:junit:$rootProject.ext.dependencies.junit"

    androidTestImplementation 'org.bouncycastle:bcprov-jdk15on:1.60'
    androidTestImplementation 'org.mindrot:jbcrypt:0.4'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation(group: 'com.android.support.test.espresso', name: 'espresso-core', version: rootProject.ext.dependencies.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

apply from: "$rootDir/gradle/publish.gradle"
